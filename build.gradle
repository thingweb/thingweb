apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

mainClassName = "de.webthing.launcher.Launcher"
sourceCompatibility = 1.8
version = '1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'thingweb',
                   'Implementation-Version': '0.1'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    compile 'com.nanohttpd:nanohttpd:2.1.1'
    compile 'org.eclipse.californium:californium-core:1.0.0-M3'
    compile 'org.json:json:20141113'
    compile 'com.github.jsonld-java:jsonld-java:0.6.0'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'
    compile group: 'net.java.dev.jna', name: 'jna-platform', version: '4.1.0'


    compile files("lib/neopixel-java-1.0-SNAPSHOT.jar")
    runtime files("lib/jsonld-context.jar")

    testCompile group: 'junit', name: 'junit', version: '4.+'
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

def createScript(project, mainClass, name) {
    project.tasks.create(name: name, type: CreateStartScripts) {
        outputDir       = new File(project.buildDir, 'scripts')
        mainClassName   = mainClass
        applicationName = name
        classpath       = project.tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + project.configurations.runtime
    }
    project.tasks[name].dependsOn(project.jar)

    project.applicationDistribution.with {
        into("bin") {
            from(project.tasks[name])
            fileMode = 0755
        }
    }
}

createScript(project, 'de.webthing.gui.ThingsClient', 'wot-client')
